allprojects {
    defaultTasks 'clean', 'build'
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew scripts for NIX and win envs'
    gradleVersion = '1.10'
}



subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'idea'
    apply from: "${rootDir}/libraries.gradle"

    group = 'org.suggs.build-sandbox'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile libs.slf4j_api

        testCompile libs.junit

        testRuntime libs.slf4j_log4j,
                libs.log4j
    }

    jar {
        manifest {
            attributes 'Implementation-Title': name, 'Implementation-Version': version
        }
    }

    test {
        useJUnit {
            includeCategories 'support.UnitTest'
        }
    }

    task integrationTest(type: Test, dependsOn: test) {
        useJUnit {
            includeCategories 'support.IntegrationTest'
        }
    }

    check.dependsOn integrationTest
}

project("rds-api") {

    description = 'The API library for the multi module build'
    dependencies {}

}

project("rds-impl") {

    description = "The implemenation of the APIs defined in the API project"
    dependencies {
        compile project(':rds-api')
    }

}
